apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    }
}

task javadoc(type: Javadoc) {
    source = project.android.sourceSets.main.java.srcDirs
    options.linkSource true
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Optimove official Android SDK'
                    url = 'https://github.com/optimove-tech/Optimove-SDK-Android'
                    licenses {
                        license {
                            name = 'Stream License'
                            url = 'https://github.com/optimove-tech/Optimove-SDK-Android/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'concerta'
                            name = 'Konstantin Antipochkin'
                            email = 'konstantin_a@optimove.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/optimove-tech/Optimove-SDK-Android.git'
                        developerConnection = 'scm:git:ssh://github.com/optimove-tech/Optimove-SDK-Android.git'
                        url = 'https://github.com/optimove-tech/Optimove-SDK-Android.git/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}